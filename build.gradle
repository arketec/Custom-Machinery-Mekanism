plugins {
    id "dev.architectury.loom" version "1.3.+"
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "maven-publish"
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    // use this if you are using the official mojang mappings
    // and want loom to stop warning you about their license
    silentMojangMappingsLicense()

    // since loom 0.10, you are **required** to use the
    // "forge" block to configure forge-specific features,
    // such as the mixinConfigs array or datagen
    forge {
        // specify the mixin configs used in this mod
        // this will be added to the jar manifest as well!
        mixinConfigs = [
                "custommachinerymekanism.mixins.json"
        ]

        runs {
            client {
                property 'mixin.env.remapRefMap', 'true'
                property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            }
        }

        // missing access transformers?
        // don't worry, you can still use them!
        // note that your AT *MUST* be located at
        // src/main/resources/META-INF/accesstransformer.cfg
        // to work as there is currently no config option to change this.
        // also, any names used in your access transformer will need to be
        // in SRG mapped ("func_" / "field_" with MCP class names) to work!
        // (both of these things may be subject to change in the future)

        // this will create a data generator configuration
        // that you can use to automatically generate assets and data
        // using architectury loom. Note that this currently *only* works
        // for forge projects made with architectury loom!
        dataGen {
            mod project.mod_id
        }
    }
}

architectury {
    forge()
}

repositories {
    maven {
        name = "Custom Machinery maven"
        url = "https://maven.frinn.fr/repository/maven/"
        content {
            includeGroup "frinn.custommachinery"
        }
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "Mod maven"
        url = "https://modmaven.dev/"
        content {
            includeGroup "mcjty.theoneprobe"
            includeGroup "mekanism"
        }
    }
    maven {
        name = "Shedaniel maven"
        url = "https://maven.architectury.dev/"
    }
    maven {
        name = "Saps maven"
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.ftb.mods"
        }
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
        content {
            includeGroup "mezz.jei"
        }
    }
    maven {
        name = "ldtteam Maven"
        url = "https://ldtteam.jfrog.io/ldtteam/modding/"
        content {
            includeGroup "com.communi-suggestu.saecularia-caudices"
        }
    }
    maven {
        name = "Curse maven"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    // Minecraft
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    // Official mappings
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
    }

    // Architectury
    modApi("dev.architectury:architectury-forge:${project.architectury_version}")
    modApi("me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}")

    // Forge
    forge "net.minecraftforge:forge:${project.forge_version}"

    // Custom Machinery
    api "curse.maven:custommachinery-fork-1058446:${project.custommachinery_file_id}"
    //api files("libs/CustomMachinery-forge-1.20.1-0.10.0.jar")
    modLocalRuntime modCompileOnly("curse.maven:custommachinery-fork-1058446:${project.custommachinery_file_id}")
    //modLocalRuntime modCompileOnly(files("libs/CustomMachinery-forge-1.20.1-0.10.0.jar"))

    //JEI
    modCompileOnly("mezz.jei:jei-${project.minecraft_version}-common-api:${project.jei_version}")
    modCompileOnly("mezz.jei:jei-${project.minecraft_version}-forge-api:${project.jei_version}")
    modLocalRuntime("mezz.jei:jei-${project.minecraft_version}-forge:${project.jei_version}") {transitive = false}

    //Mekanism
    modImplementation "mekanism:Mekanism:${project.minecraft_version}-${project.mekanism_version}.${project.mekanism_build}"

    //KubeJS
    modLocalRuntime modCompileOnly("dev.latvian.mods:kubejs-forge:${rootProject.kubejs_version}")
    localRuntime("io.github.llamalad7:mixinextras-forge:0.2.0-rc.4")

    //The one probe
    modLocalRuntime("curse.maven:the-one-probe-245211:${project.top_file_id}") {transitive = false}

    //Jade
    modLocalRuntime modCompileOnly("curse.maven:jade-324717:5339264")
}

processResources {
    // define properties that can be used during resource processing
    inputs.property "version", project.version

    // this will replace the property "${version}" in your mods.toml
    // with the version you've defined in your gradle.properties
    filesMatching("META-INF/mods.toml") {
        expand "license": project.license,
        "mod_version": project.mod_version,
        "mod_name": project.mod_name,
        "author": project.author,
        "description": project.mod_description,
        "minecraft_version": project.minecraft_version,
        "custommachinery_version": project.custommachinery_version,
        "mekanism_version": project.mekanism_version
    }
}

tasks.withType(JavaCompile) {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    options.encoding = "UTF-8"
    options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    // add some additional metadata to the jar manifest
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.mod_name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.archives_base_name
            version = "${project.minecraft_version}-${project.mod_version}"
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            pom {
                name = project.mod_name
                description = project.mod_description
                packaging = 'jar'
                scm {
                    url = project.github
                }
                issueManagement {
                    system = 'github'
                    url = project.issues
                }
                licenses {
                    license {
                        name = project.license
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = project.author
                        name = project.author
                        roles = ['developer']
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'maven'
            url = 'https://maven.frinn.fr/repository/maven/'
            credentials {
                username = 'frinn'
                password = System.getenv('MAVEN_PASSWORD')
            }
        }
    }

    unifiedPublishing {
        project {
            displayName = "Custom-Machinery-Mekanism-${project.minecraft_version}-${project.mod_version}"
            releaseType = "release"
            if(rootProject.file("CHANGELOG.md").exists()) {
                changelog = rootProject.file("CHANGELOG.md").text
            }
            gameVersions = ["${project.minecraft_version}"]
            gameLoaders = ["forge"]
            mainPublication remapJar

            var CURSE_API_KEY = System.getenv("CURSEFORGE")
            if (CURSE_API_KEY != null) {
                curseforge {
                    token = CURSE_API_KEY
                    id = "721079"
                    gameVersions.addAll "Java 17"
                    relations {
                        depends "custom-machinery"
                        depends "mekanism"
                        optional "crafttweaker"
                        optional "jei"
                        optional "the-one-probe"
                        optional "kubejs"
                        optional "jade"
                    }
                }
            }

            var MODRINTH_TOKEN = System.getenv("MODRINTH")
            if (MODRINTH_TOKEN != null) {
                modrinth {
                    token = MODRINTH_TOKEN
                    id = "VFxg3xmP"
                    relations {
                        depends "custom-machinery"
                        depends "mekanism"
                        optional "kubejs"
                    }
                }
            }
        }
    }
}
